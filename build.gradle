apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

def flutterProjectRoot = new File("./flutter")

def plugins = new Properties()
def pluginsFile = new File(flutterProjectRoot, '.flutter-plugins')
if (pluginsFile.exists()) {
    pluginsFile.withReader('UTF-8') { reader -> plugins.load(reader) }
}

def pluginsAarDirs = []

plugins.each { name, path ->
    def pluginAarDirectory = flutterProjectRoot.toPath().resolve(path).resolve('android').resolve("build").resolve("outputs").resolve("aar").toFile()
    pluginsAarDirs.add(pluginAarDirectory)
}

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
    }
}

repositories {
    flatDir {
        pluginsAarDirs.add("./flutter/.android/Flutter/build/outputs/aar")
        dirs = pluginsAarDirs
    }
    //mavenLocal() must be listed at the top to facilitate testing
    mavenLocal()
    google()
    mavenCentral()
    maven { url "https://jitpack.io" }
    jcenter()
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'android/main/AndroidManifest.xml'
            java.srcDirs = ['android/main/java']
            resources.srcDirs = ['android/main/java']
            aidl.srcDirs = ['android/main/java']
            renderscript.srcDirs = ['android/main/java']
            res.srcDirs = ['android/main/res']
            assets.srcDirs = ['android/main/assets']
        }
    }
}

apply from: 'https://raw.githubusercontent.com/kerry/gradle-fat-aar/master/fat-aar.gradle'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    embedded name: "flutter-release", ext: "aar"
    // Flutter Plugins
    embedded name: "path_provider-release", ext: "aar"

    implementation 'androidx.appcompat:appcompat:1.0.0-alpha1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha3'
}

task buildFlutter {
    doLast {
        exec {
            workingDir "./flutter/.android"
            commandLine("./gradlew", "assembleRelease")
        }
    }
}

publishing {
    publications {
        flutter(MavenPublication) {
            groupId 'com.upgrad'
            artifactId 'UGFlutter'
            version '0.0.19'
            artifact("$buildDir/outputs/aar/UGFlutter-release.aar")
        }
    }
    repositories {
        mavenLocal()
    }
}

project.afterEvaluate {
    preBuild.dependsOn buildFlutter
    publishToMavenLocal.dependsOn assembleRelease
}